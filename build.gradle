plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
    id 'jacoco'
    id 'maven-publish'
}

group = 'cl.benm'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2'
    testImplementation 'org.mockito:mockito-core:5.4.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.0.0'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(11)
}



test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'cl/benm/jsons/exception/**',
                'cl/benm/jsons/config/**'
            ])
        }))
    }
}


jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.75
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/BenMMcLean/JSON-Specialiser")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}